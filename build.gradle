buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }

        // Sponge Repository
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"

        // For Mixins
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'

version = "2.0.0-SNAPSHOT"
group = "me.boomboompower.skinchanger"
archivesBaseName = "SkinChanger"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.1722"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_22"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()

    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }

    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    provided('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    from configurations.provided.asFileTree.files.collect {
        zipTree(it)
    }

    manifest.attributes(
            'Main-Class' : 'me.boomboompower.skinchanger.run.JavaMain',
            'MixinConfigs' : 'mixins.skinchanger.json',
            'TweakOrder' : '0',
            'TweakClass' : 'me.boomboompower.skinchanger.mixins.Tweaker',
            'FMLCorePlugin' : 'me.boomboompower.skinchanger.mixins.Tweaker'
    )
}

configurations {
    provided
    compile.extendsFrom(provided)
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.skinchanger.refmap.json"
}
