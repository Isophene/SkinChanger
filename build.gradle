/*
 *     Copyright (C) 2020 boomboompower
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }

        // Sponge Repository
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"

        // For Mixins
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'

version = '3.0.0'
group = "me.do_you_like.skinchanger"
archivesBaseName = "SkinChangerMod"

// We use Java 8
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    // Default run directory will be located in 'PROJECT_DIR/run'
    runDir = "run"

    // Version retrieved off https://files.minecraftforge.net/
    version = "1.8.9-11.15.1.1722"

    // The mapping to use http://export.mcpbot.bspk.rs/stable/
    mappings = "stable_22"
}

repositories {
    mavenCentral()

    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        url "https://jitpack.io"
    }
}

// General mod dependencies, we rely on Gson, Lombok and Mixin
dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // Add the Mixin library to our jar file but exclude modules which
    // Already exist in vanilla minecraft and in forge.
    provided('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

    compile 'org.projectlombok:lombok:1.16.20'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

// Makes the output jar name contain no versioning scheme so the build script
// can find the jar easily without hardcoding a version inside of the buildscript.
//
// Fixes an issue with Mixin where it tries to add a class to the jar which
// is already inside of the jar (I honestly have no idea why this occurs).
//
// Also places all valid META-INF/MANIFEST.MF information inside of the mod such as
// The mixin tweaker location & the installer main class.
jar {
    archiveName = "$baseName.$extension"

    from configurations.provided.asFileTree.files.collect {
        // Fixes an issue where Mixin's copies a duplicate file into the build jar...
        exclude 'META-INF/services/javax.annotation.processing.Processor'

        zipTree(it)
    }

    manifest.attributes(
            'Main-Class' : 'me.do_you_like.mods.skinchanger.installer.InstallerCore',
            'MixinConfigs' : 'mixins.skinchanger.json',
            'TweakOrder' : '0',
            'TweakClass' : 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLCorePluginContainsFMLMod' : true,
            'ForceLoadAsMod' : true
    )
}

// A simple patch to the sourceJar so it does not contain the
// version information. The installer will place the versioned file
// in the mods directory but we must exclude the version so the build
// script on github works properly.
sourceJar {
    // Rename the jar so it doesn't contain "SNAPSHOT"
    archiveName = "$baseName-sources.$extension"
}

sourceSets {
    //noinspection GroovyAssignabilityCheck
    main {
        // This is just to get Mixin working in dev environment.
        // The class is just a simple Tweaker class similar to
        // MixinTweaker however it contains some credentials
        // Which is why I've excluded it here. If you need help settings
        // this up don't hesitate to message me anywhere.
        java {
            exclude '**/priv'
        }

        // Groovy does not like that refMap is referred to as a string
        // Which is why I suppress the warning (it still works as a string).
        // noinspection GroovyAssignabilityCheck
        refMap = "mixins.skinchanger.refmap.json"
    }
}

// Makes the "compile" configuration extend from the compile
// configuration (this makes it inherit similar build properties)
configurations {
    provided
    compile.extendsFrom(provided)
}

// Tells Mixin which environment we are building in.
// Specify "notch" or "searge" here
// "notch" = obfuscated
// "searge" = MCP mappings
mixin {
    defaultObfuscationEnv searge
}
